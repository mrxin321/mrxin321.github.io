---
layout: post
category: "read"
title:  "每日回顾"
tags: [生命在于折腾不止]
---


### 关于溶解shader

溶解效果在游戏中是很常见的，比如在一些神话或者魔法世界中,一些NPC角色在剧情需要时候会身体会渐渐的消失掉.甚至有一些更炫的,比如用火焰喷射器把目标燃尽。这些都可以用到溶解效果。这篇文章主要是讲解一下比较基础的溶解效果如何实现，实现的方法并不唯一,本篇只是其中一种思路。

原理

 

　　既然想让角色的身体一块块渐渐消失，不妨就让角色身体上相应的部位不进行渲染(或者改成透明,我们这里选择前者)。那根据什么来判断身体的哪一部分需要被溶解呢,这时候就需要一张额外的贴图或者利用角色纹理贴图的Alpha通道(本篇选择前者)。这个贴图和纹理贴图一样，对应着玩家身体的每一个位置，这样我们就可以根据贴图上某个指定通道的颜色值来控制角色各个身体部位是否溶解了。

　　另外我们还会涉及到的一个命令叫discard，是由CG提供的,若出现在fragmentShader中表示立即放弃当前处理的片元。也就是说当我们判定当前片远需要溶解的时候我们就使用discard命令。


　上面这张图就是用来控制溶解程度的纹理，我们这里比较简单，只使用了R通道，如果你想做的很复杂也可以利用上其它的通道。在本篇中我们将根据时间的推进不断溶解掉贴图上R通道颜色值较小的区域。美术可以利用这张贴图控制角色的任意溶解顺序和效果。由于我的素材都是随便找的，自己又不会画画，所以这个图和角色纹理可能并不搭配，请不要在意。

　　再看下面这个图,我先来提前说一下本篇Shader在Properties中提供的各个可调节的参数

1.Base(RGB)是角色纹理贴图，不用解释了.

2.NoiseTex(R)是很重重要的,我们用来控制角色溶解的样式贴图，我们只利用了R通道。

3.DissolveSpeed(Second),整个溶解过程需要的时间,单位是秒

4.EdgeWidth,这个就是额外加的一个边缘效果，比如你观察纸在化作灰烬时他的周围会先变成黑褐色。我们这里的EdgeWidth就是定义这个周围区域的大小,注意这个width并不是指的长度,而是定义一个透明度的间隔区间,也就是与基准值相差多少可以算作边缘处理。

5.EdgeColor,和4一样，边缘效果的颜色。


### 实现

Shader "Esfog/Dissolve" 
{
    Properties 
    {
        _MainTex ("Base (RGB)", 2D) = "white" {}
        _NoiseTex ("NoiseTex (R)",2D) = "white"{}
        _DissolveSpeed ("DissolveSpeed (Second)",Float) = 1
        _EdgeWidth("EdgeWidth",Range(0,0.5)) = 0.1
        _EdgeColor("EdgeColor",Color) =  (1,1,1,1)
    }
    SubShader 
    {
        Tags { "RenderType"="Opaque" }
        
        Pass
        {
            CGPROGRAM
            #pragma vertex vert_img
            #pragma fragment frag
            #include "UnityCG.cginc"
             
            uniform sampler2D _MainTex;
            uniform sampler2D _NoiseTex;
            uniform float _DissolveSpeed;
            uniform float _EdgeWidth;
            uniform float4 _EdgeColor;
            
            float4 frag(v2f_img i):COLOR
            {
                float DissolveFactor = saturate(_Time.y / _DissolveSpeed);
                float noiseValue = tex2D(_NoiseTex,i.uv).r;            
                if(noiseValue <= DissolveFactor)
                {
                    discard;
                }
                
                float4 texColor = tex2D(_MainTex,i.uv);
                float EdgeFactor = saturate((noiseValue - DissolveFactor)/(_EdgeWidth*DissolveFactor));
                float4 BlendColor = texColor * _EdgeColor;
                                
                return lerp(texColor,BlendColor,1 - EdgeFactor);
            }
            
            ENDCG
        }
    } 
    
    FallBack Off
}



　好了基本效果就是如此，总体来说这个Shader的效果看起来好与不好，主要还是取决于美术提供的溶解贴图是否合适,本节素材并不是一套的，看起来会有点怪。

　　另外一点就是大家在学习Shader的过程中你总会发现别人打源代码中会出现很多pow,lerp,或者对各个参数的加减乘除，希望大家不要太纠结这些地方的意义，有时候确实是有一定的数学原理,但很多时候都是开发者自己写的一个经验公式，为了调效果而写的。随着你的经验慢慢增多，渐渐的你也会开始使用一些经验公式。效果对了就好，只要不影响性能。毕竟给玩家看的又不是源代码。

